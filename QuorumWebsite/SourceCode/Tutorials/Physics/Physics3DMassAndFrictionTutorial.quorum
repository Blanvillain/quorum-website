use Libraries.Web.Page.all
use Libraries.Containers.Array

class Physics3DMassAndFrictionTutorial is Lesson
    text dq = ""
    dq = dq:GetDoubleQuote()
    text cr = ""
    cr = cr:GetCarriageReturn()
    Preformatter preformatter //helper

    action Setup
        LineBreak break //will be used multiple times
        
        //html header stuff
        SetWebPageTitle("Physics Mass, Friction and More In 3D - Quorum Programming Language")
        AddDefaultHeaders()

         //page header
        Array<Division> divs = InitializeTutorialPage()
        Division bodyDiv = divs:Get(1)
        Division onPageDiv = divs:Get(2)

        Header headDiv
        headDiv:SetClassAttribute("flex flex-col gap-y-2")
        Heading h_heading = headDiv:AddHeading("Physics Mass, Friction and More In 3D", 1)
        h_heading:AddClassAttribute("text-3xl font-bold")
        h_heading:AddAttribute("data-text", "Physics Mass, Friction and More In 3D")
        AddToHeadingArray(h_heading)

        headDiv:AddText("Understanding mass, friction, and more in a 3D physics enabled Quorum game")
        bodyDiv:Add(headDiv)


        //the section on general information
        Section section_overview
        section_overview:AddClassAttribute("flex flex-col gap-y-2")
        bodyDiv:Add(section_overview)
        AddHeading(section_overview, "Overview", 2)


        Paragraph para1
     
        para1:AddText(    "In this tutorial, we will create a game where a Quorum bunny crashes into a tower of boxes with its deadly hops. "
                            +   "Along the way, we will learn about new physics properties and concepts. "
                            +   "The first concepts necessary for the bunny to knock over the boxes are ")
        Link link1 = para1:AddLink(    "mass", "https://en.wikipedia.org/wiki/Mass")
        link1:SetClassAttribute("underline text-quorum-blue-150")
        para1:AddText(    " and ")
        Link link2 = para1:AddLink(    "density", "https://en.wikipedia.org/wiki/Density")
        link2:SetClassAttribute("underline text-quorum-blue-150")
        para1:AddText(    ". ")
        section_overview:Add(para1)

        //the section on Mass and Density
        Section section_mass
        section_mass:AddClassAttribute("flex flex-col gap-y-2")
        bodyDiv:Add(section_mass)
        AddHeading(section_mass, "Mass and Density", 2)
        section_mass:AddParagraph(   "Up until now, all physics objects’ masses have been determined using a default mass of 1 kilogram. "
                            +   "We can change this by setting either mass or density. "
                            +   "Mass is how much stuff something is made of. "
                            +   "It is related to weight. "
                            +   "As mass increases, so does weight. "
                            +   "Weight also relies on gravity (think of astronauts on the moon and how they weigh less there even though they have not changed themselves.) "
                            +   "We can think of mass as how much our objects weigh as long as we remember that changing gravity will also affect their weight. "
                            +   "Mass is set per item using this action: ")
        section_mass:Add(preformatter:GeneratePreformattedCode(  "number mass = 5" + cr
                                                            + "itemName:SetMass(mass)"))
        Paragraph para2
        para2:AddText(    "Here mass is a number representing how many kilograms an item is. "
                            +   "To help us understand the units of mass better, consider a few examples. "
                            +   "The mass of a typical person is about 70 kg. "
                            +   "The mass of a car might be between 1000 to 2000 kg, and the mass of a commercial airliner might be between about 40,000 kg and 75,000 kg. "
                            +   "When mass is set this way, density is automatically calculated. "
                            +   "Density is another way to think about mass. "
                            +   "It is how much mass an item has per units cubed, where a unit is a length of one. "
                            +   "By units cubed, we mean a one unit by one unit by one unit cube. "
                            +   "We can set density for an item using: ")
        section_mass:Add(para2)

         section_mass:Add(preformatter:GeneratePreformattedCode(  "number density = 5" + cr
                                                            + "itemName:SetDensity(density)"))
         section_mass:AddParagraph(    "density is a number representing the number of kilograms per units cubed. "
                            +   "If density is set like this, mass will be automatically calculated for the item. ")
        
        //the section on friction
        Section section_friction
        section_friction:AddClassAttribute("flex flex-col gap-y-2")
        bodyDiv:Add(section_friction)
        AddHeading(section_friction, "Friction", 2)

        Paragraph para3
 
        para3:AddText(    "In the previous tutorial on ")
        Link link3 = para3:AddLink(    "force and velocity", "/tutorials/physics/forceandvelocity3D.html")
        link3:SetClassAttribute("underline text-quorum-blue-150")
        para3:AddText(    ", the character was able to move across the platforms without slowing down. "
                            +   "The physics property that provides resistance to movement across something is called ")
        para3:AddLink("friction", "https://en.wikipedia.org/wiki/Friction")
        para3:AddText(    ". Sliding on ice or a puck moving in air hockey are examples of movement with very little friction. "
                            +   "Pushing furniture on heavy carpet is an example of movement with high friction. "
                            +   "To set an item’s friction we use: ")

        section_friction:Add(preformatter:GeneratePreformattedCode(  "number friction = 0.5" + cr
                                                            + "itemName:SetFriction(friction)"))
        section_friction:AddParagraph(   "Here friction is a number between zero and one. "
                            +   "Zero means no friction and one is the highest amount of friction. "
                            +   "If we set friction to be a number higher than one, the system will treat it the same as if we set it to one. ")
        
        //the section on Restitution
        Section section_restitution
        section_restitution:AddClassAttribute("flex flex-col gap-y-2")
        bodyDiv:Add(section_restitution)
        AddHeading(section_restitution, "Restitution", 2)

        Paragraph para4
        para4:AddText(    "We want our bunny to be relatively bouncy. "
                            +   "To achieve this, we need a new physics property called ")
        Link link4 = para4:AddLink(    "restitution", "https://en.wikipedia.org/wiki/Coefficient_of_restitution")                    
        link4:SetClassAttribute("underline text-quorum-blue-150")
        para4:AddText(    ". Restitution can be thought of as the bounciness of an item. "
                            +   "Like friction, restitution is set per item using: ")
        section_restitution:Add(para4)

        section_restitution:Add(preformatter:GeneratePreformattedCode(  "number restitution = 0.4" + cr
                                                            + "itemName:SetRestitution(restitution)"))
        Paragraph para5
        para5:AddText(    "Here restitution is a number representing how much bounce is preserved on impact. "
                            +   "To understand this fully, we need a new concept: ")
         Link link5 = para5:AddLink(    "energy", "https://en.wikipedia.org/wiki/Energy") 
          link5:SetClassAttribute("underline text-quorum-blue-150")                    
        para5:AddText(    ". When an item falls, the movement is a result of gaining ")
         Link link6 = para5:AddLink(    "kinetic energy", "https://en.wikipedia.org/wiki/Kinetic_energy")
          link6:SetClassAttribute("underline text-quorum-blue-150")
        para5:AddText(    ". Kinetic means movement, basically. "
                            +   "When the item is at the height of its fall before starting to move downward, it has no kinetic energy (it is not moving). "
                            +   "Instead it has ")
         Link link7 = para5:AddLink(    "potential energy", "https://en.wikipedia.org/wiki/Potential_energy")
          link7:SetClassAttribute("underline text-quorum-blue-150")
        para5:AddText(    ". During the fall, the item trades its potential energy for kinetic energy. "
                            +   "When the item starts to bounce back up, it is trading its kinetic energy for potential energy. "
                            +   "The restitution value, then, is multiplied by the energy at impact to affect how much kinetic energy the item has when it starts to bounce up. "
                            +   "For example, a restitution of one means that the item retains all of its kinetic energy when it bounces. "
                            +   "A restitution of zero means that the item loses all its kinetic energy, and therefore does not bounce. "
                            +   "Realistic restitution, then, would be somewhere between zero and one, but it is possible to have a restitution greater than one. ")
       section_restitution:Add(para5)
         
        //the section containing the platformer game code
        Section section_code
        section_code:AddClassAttribute("flex flex-col gap-y-2")
        bodyDiv:Add(section_code)
        AddHeading(section_code, "Code", 2)

        Paragraph para6
        para6:AddText(    "Here is a ")
        Link link8 = para6:AddLink(    "zip file" , "/files/games/Mass3D.zip" )
        para6:AddText(    " containing a Quorum project that uses the code below. "
                            +   "Read through, then use the following code to discover mass, friction, and restitution. ")
        link8:SetClassAttribute("underline text-quorum-blue-150")
        section_code:Add(para6)

        CodeBlock cb1
        Span s1 
        Span s2 
        Span s3
        Span s4
        Span s5
        cb1:AddText("use Libraries.Game.Game" + cr 
                    +   "use Libraries.Game.Graphics.Model"+ cr 
                    +   "use Libraries.Compute.Vector3" + cr 
                    +   "use Libraries.Game.Graphics.Color" + cr 
                    +   "use Libraries.Interface.Events.KeyboardListener" + cr 
                    +   "use Libraries.Interface.Events.KeyboardEvent" + cr
                    +   "use Libraries.Interface.Events.CollisionListener3D" + cr
                    +   "use Libraries.Interface.Events.CollisionEvent3D" + cr
                    +   "use Libraries.Sound.Audio" + cr
                    +   "use Libraries.Compute.Math" + cr + cr
                    +   "class Main is Game, KeyboardListener, CollisionListener3D" + cr + cr
                    +   "   Math math" + cr   
                    +   "   Model ground" + cr
                    +   "   Model bunny" + cr + cr
                    +   "   number depth = 8" + cr
                    +   "   number length = 20" + cr
                    +   "   number height = -3" + cr + cr
                    +   "   Vector3 gravity" + cr
                    +   "   Vector3 character" + cr
                    +   "   Color color" + cr + cr
                    +   "   Audio jumpSound" + cr
                    +   "   Audio impactSound" + cr
                    +   "   Audio movingSound" + cr + cr 
                    +   "   action Main" + cr 
                    +   "       StartGame()" + cr 
                    +   "   end" + cr + cr
                    +   "   action CreateGame" + cr
                    +   "       AddCollisionListener(me)" + cr
                    +   "       AddKeyboardListener(me)" + cr
                    +   "       EnablePhysics3D(true)" + cr + cr
                    +   "       ground:SetName(" + dq + "ground" + dq +")" + cr
                    +   "       ground:LoadBox(length, 1, length, color:Green())" + cr
                    +   "       ground:SetPosition(0, height, depth)" + cr
                    +   "       Add(ground)" + cr
                    +   "       ground:EnablePhysics(true)" + cr
                    +   "       ground:SetUnmovable()" + cr 
                    +   "       ground:SetFriction(1)" + cr + cr
                    +   "       bunny:SetName(" + dq + "bunny" + dq + ")" + cr
                    +   "       bunny:LoadSphere(1, 1, 1, " + dq + "media/HourOfCodeDark.jpg" + dq + ")" + cr
                    +   "       bunny:SetPosition(-5, height + 0.5, 0)" + cr
                    +   "       Add(bunny)" + cr
                    +   "       bunny:EnablePhysics(true)" + cr
                    +   "       bunny:SetResponsive()" + cr + cr
                    +   "       bunny:SetFriction(0.2)" + cr
                    +   "       bunny:SetRestitution(0.75)" + cr
                    +   "       bunny:SetMass(2)" + cr + cr
                    +   "       gravity:Set(0, -10, 0)" + cr
                    +   "       SetGravity3D(gravity)" + cr + cr)
        s4:AddText("    // We make boundary walls so that everything stays on screen. " + cr)
        s4:AddClassAttribute("comment")
        cb1:Add(s4)
        cb1:AddText(    "       CreateBoundaryWall(-length/2, height, depth)" + cr
                    +   "       CreateBoundaryWall(length/2, height, depth)" + cr
                    +   "       CreateBoundaryWall(0, height, length/2  + depth/2)" + cr 
                    +   "       CreateBoundaryWall(0, length + 1, depth)" + cr + cr)
        s5:AddText("    // We create a tower of boxes. " + cr)
        s5:AddClassAttribute("comment")
        cb1:Add(s5)
        cb1:AddText(    "       GenerateBox(0.7,1,color:Red())" + cr
                    +   "       GenerateBox(-0.7,1,color:Red())" + cr
                    +   "       GenerateBox(0,2, color:Yellow())" + cr
                    +   "       GenerateBox(0.7,3,color:Blue())" + cr
                    +   "       GenerateBox(-0.7,3,color:Blue())" + cr
                    +   "       GenerateBox(0,4, color:Purple())" + cr + cr
                    +   "       jumpSound:Load(" + dq + "media/Fwip.ogg" + dq + ")" + cr
                    +   "       impactSound:Load(" + dq + "media/Boing.ogg" + dq + ")" + cr
                    +   "       movingSound:Load(" + dq + "media/Bing.ogg" + dq + ")" + cr + cr
                    +   "       jumpSound:SetVolume(0.5)" + cr
                    +   "       impactSound:SetVolume(0.3)" + cr
                    +   "   end" + cr + cr
                    +   "   action Update(number seconds)" + cr)
        s3:AddText("    // If the bunny is noticably moving, we play a sound to represent the bunny's position. " + cr)
        s3:AddClassAttribute("comment")
        cb1:Add(s3)
        cb1:AddText(   "       if math:AbsoluteValue(bunny:GetLinearVelocity():GetX()) > 0.5 or math:AbsoluteValue(bunny:GetLinearVelocity():GetY()) > 0.5" + cr
                    +   "           movingSound:SetBalance(bunny:GetX())" + cr
                    +   "           movingSound:SetPitch(bunny:GetY())" + cr
                    +   "           movingSound:Play()" + cr
                    +   "       end" + cr
                    +   "   end" + cr + cr
                    +   "   action PressedKey(KeyboardEvent event)" + cr
                    +   "       if event:keyCode = event:SPACE" + cr
                    +   "           Vector3 force" + cr
                    +   "           force:Set(450, 860, 450)" + cr
                    +   "           bunny:ApplyForceToCenter(force)" + cr
                    +   "           jumpSound:Play()" + cr
                    +   "       end" + cr
                    +   "   end" + cr + cr
                    +   "   action BeginCollision(CollisionEvent3D event)" + cr
                    +   "       impactSound:Play()" + cr
                    +   "   end" + cr + cr)
        s1:AddText( "   /*" + cr
                    +   "   To balance the tower of boxes, we make every other level have wider boxes. The" + cr
                    +   "   displaceFromGround parameter tells us how many levels of boxes up to place a box. For " + cr
                    +   "   example, if it equals 2, then the box is placed two box heights up from the bottom of " + cr
                    +   "   the screen. displaceFromCenter tells us how many box lengths from the center we want " + cr
                    +   "   the box to be placed horizontally. For example, if we want two boxes centered ontop of " + cr
                    +   "   a box then we can use displaceFromCenter = 0.7 for one and -0.7 for the other. " + cr
                    +   "   This will place them so that the gap between them is 0.7 box lengths from the center. " + cr
                    +   "   */" + cr)
        s1:SetClassAttribute("comment")
        cb1:Add(s1)
        cb1:AddText("   action GenerateBox(number displaceFromCenter, integer displaceFromGround, Color color)" + cr
                    +   "       Model box" + cr
                    +   "       number boxSize = 1.5" + cr 
                    +   "       number center = 3" + cr + cr
                    +   "       if displaceFromGround mod 2 = 0" + cr
                    +   "           box:LoadBox(boxSize * 3.5, boxSize, boxSize * 1.5, color)" + cr
                    +   "           box:SetPosition(center + displaceFromCenter*(boxSize * 1.5), (boxSize )*displaceFromGround + height + boxSize/2, 7)" + cr 
                    +   "       else" + cr
                    +   "           box:LoadBox(boxSize * 1.5, boxSize, boxSize * 1.5, color)" + cr
                    +   "           box:SetPosition(center + displaceFromCenter*(boxSize * 1.5), (boxSize)*displaceFromGround + height + boxSize/2, 7)" + cr
                    +   "       end" + cr
                    +   "       Add(box)" + cr
                    +   "       box:EnablePhysics(true)" + cr
                    +   "       box:SetFriction(1)" + cr
                    +   "       box:SetResponsive()" + cr
                    +   "       box:SetMass(0.3)" + cr
                    +   "   end" + cr + cr
                    +   "   action CreateBoundaryWall(number x, number y, number z)" + cr
                    +   "       Model boundaryWall" + cr)
        s2:AddText( "   /*" + cr
                    +   "   We need two invisible walls for the sides that are taller than long, one for the top that is longer than tall," + cr
                    +   "   and one in the back that is about equally long and tall. The side walls' positions both have y = 0, and the back " + cr
                    +   "   wall is the only one with z > depth. That is how we can tell which type is called for. " + cr
                    +   "   */" + cr)
        s2:SetClassAttribute("comment")
        cb1:Add(s2)
        cb1:AddText("       if z > depth" + cr
                    +   "           boundaryWall:LoadBox(length + 2, length, 1, color:White())" + cr
                    +   "       elseif y <= 0" + cr
                    +   "           boundaryWall:LoadBox(1, length, length + 1, color:White())" + cr
                    +   "       else" + cr
                    +   "           boundaryWall:LoadBox(length, 1, length + 1, color:White())" + cr
                    +   "       end" + cr
                    +   "       boundaryWall:SetPosition(x , y, z)" + cr
                    +   "       Add(boundaryWall)" + cr
                    +   "       boundaryWall:EnablePhysics(true)" + cr
                    +   "       boundaryWall:SetUnmovable()" + cr
                    +   "   end" + cr
                    +   "end")
        section_code:Add(preformatter:GeneratePreformattedCode(cb1))

        //the section on running the program
 Section section_try_yourself
    section_try_yourself:AddClassAttribute("flex flex-col gap-y-2")
    bodyDiv:Add(section_try_yourself)
    Heading h2_try_yourself = section_try_yourself:AddHeading("Try it Yourself!", 2)
    h2_try_yourself:AddAttribute("data-text", "Try it Yourself!")
    h2_try_yourself:AddClassAttribute("text-2xl font-bold")
    AddToHeadingArray(h2_try_yourself)
    section_try_yourself:AddAttribute("data-text", "Try it Yourself!")
    section_try_yourself:AddParagraph("Press the blue run button to execute the code in the code editor. Press the red stop button to end the program. Your program will work when the console outputs "+dq+"Build Successful!"+dq+"")
    
    CodeForIDE code4
    code4:AddFileText("Main.quorum", " ")
    IDE ide3 = AddIDE()
    ide3:SetLightTheme()
    ide3:SetCodeForIDE(code4:GetJsonArray())
    ide3:SetIdeTitle("Mass, Friction, and More In 3D")
    section_try_yourself:Add(ide3)


section_try_yourself:AddParagraph(   "If we run this game, we should have a Quorum bunny sitting on the left side of the screen on the ground and a stack of boxes on the right side of the screen. "
                                +   "Pressing the spacebar will cause the bunny to jump across the screen, hitting the stack of boxes. "
                                +   "The boxes should be knocked off the stack in response to the collision. ")
       
        Section nextLesson
        nextLesson:AddClassAttribute("flex flex-col gap-y-2")
        bodyDiv:Add(nextLesson)
        WriteNextTutorial(nextLesson)    
        onPageDiv:Add(CreateHeadingTableOfContents())

    end

    action GetMetaDescription returns text
        return "Learn how to add physics effects like 3D mass and friction to games written in the Quorum programming language."
    end

    action GetSummaryName returns text
        return "Mass, Friction, and More In 3D"
    end

    action GetImageLocation returns text
        return "media/physics/Mass.png"
    end

    action GetName returns text
        return "Mass, Friction, and More In 3D"
    end

    action GetShortDescription returns text
        return "how to use mass, friction and more in a 3D game"
    end

    action GetLocation returns text
        return "tutorials/physics/massandfriction3D.html"
    end

     action GetLinkName returns text
        return "Mass, Friction, and More in 3D"
    end

    action GetSectionName returns text
        return "Game Physics"
    end
end
