
use Libraries.Web.Page.WebPage
use Libraries.Web.Page.LineBreak
use Libraries.Web.Page.MetaData
use Libraries.Web.Page.Division
use Libraries.Web.Page.UnorderedList
use Libraries.Web.Page.Bold
use Libraries.Web.Page.CodeBlock
use Libraries.Web.Page.ListItem
use Libraries.Web.Page.Paragraph
use Libraries.Web.Page.PreformattedText
use Libraries.Web.Page.Comment
use Libraries.Web.Page.Span

class AudioPlayLongerAudioTutorial is Lesson
    text dq = ""
    dq = dq:GetDoubleQuote()
    text cr = ""
    cr = cr:GetCarriageReturn()

    Preformatter preformatter //helper

    action Setup
        LineBreak break //will be used multiple times
        
        //html header stuff
        SetWebPageTitle("Playing Longer Audio - Quorum Programming Language")
        AddDefaultHeaders()

        //page header
        Division headDiv
        headDiv:SetClassAttribute("pageHeader outermostContainer")
        headDiv:AddHeading("Tutorial: Playing Longer Audio", 1)
        headDiv:AddText("This tutorial teaches you how to stream large audio files in Quorum.")
        Add(headDiv)

        Division bodyDiv
        bodyDiv:SetClassAttribute("bodyContainer")
        bodyDiv:AddHeading("Playing Longer Audio: Streaming", 2)


        bodyDiv:AddParagraph(   "If we have a longer audio file, like one that contains a full-length song, we may want to load our file via the LoadToStream action. "
                            +   "The LoadToStream action allows our audio file to be loaded in " + dq + "chunks," + dq + "as they are needed instead of all at once. "
                            +   "This is more efficient and allows the file to start playing more quickly than if it must wait for the entire audio file to be loaded, especially for longer sounds, like songs. In order to stream our Audio object, we will need a loop that keeps the Audio object streaming as long as it is playing. "
                            +   "An example of this follows: ")


        CodeBlock cb11
        Span s7
        Span s71
        Span s72
        Span s73
        Span s74
        cb11:AddText(   "use Libraries.Sound.Audio" + cr + cr)
        s7:AddText(     "// this line creates our Audio object we will be using to stream" + cr)
        s7:SetClassAttribute("comment")
        cb11:Add(s7)
        cb11:AddText(   "Audio streamingAudio" + cr + cr)

        s71:AddText(    "// this line loads a " + dq + "chunk" + dq + "of the data from the file " + dq + "LongSong.ogg" + dq + "to our Audio object" + cr)
        s71:SetClassAttribute("comment")
        cb11:Add(s71)
        cb11:AddText(   "streamingAudio:LoadToStream(" + dq + "Sounds/longSong.ogg" + dq + ")" + cr + cr)

        s72:AddText(    "// this line plays the first " + dq + "chunk" + dq + "of the data that was loaded from the file" + cr)
        s72:SetClassAttribute("comment")
        cb11:Add(s72)
        cb11:AddText(   "streamingAudio:Play()" + cr + cr)

        s73:AddText(    "// this loop will continuously check if the whole song has been played" + cr)
        s73:SetClassAttribute("comment")
        cb11:Add(s73)
        cb11:AddText(   "repeat while streamingAudio:IsPlaying()" + cr)

        s74:AddText(    "   // this gets the next " + dq + "chunk" + dq + "of data to play" + cr)
        s74:SetClassAttribute("comment")
        cb11:Add(s74)
        cb11:AddText(   "   streamingAudio:Stream()" + cr
                    +   "end"  + cr )

        bodyDiv:Add(preformatter:GeneratePreformattedCode(cb11))




        bodyDiv:AddParagraph(   "A Quorum File object can also be used instead of a text file path when calling the LoadToStream action. " + cr )
        bodyDiv:AddParagraph(   "If we are playing sound as part of a game using the Quorum Game Engine, the proper place to put the Stream() call is inside the Update action, which is called every frame in the Main Game Loop. "
                            +   "This will ensure that the next " + dq + "chunk" + dq + "of sound will be loaded at every opportunity. An example of streaming in the game engine follows: ")

        CodeBlock cb12
        Span s8
        cb12:AddText(   "use Libraries.Game.Game" + cr
                    +   "use Libraries.Sound.Audio" + cr + cr
                    +   "class Main is Game" + cr
                    +   "   Audio audio1" + cr 
                    +   "   Audio song" + cr + cr
                    +   "   action Main" + cr 
                    +   "       StartGame()" + cr
                    +   "   end" + cr + cr
                    +   "   action CreateGame" + cr
                    +   "       audio1:Load(" + dq + "Sounds/Fwip.ogg" + dq + ")" + cr
                    +   "       audio1:Play()" + cr
                    +   "       song:LoadToStream(" + dq + "Sounds/song.ogg" + dq + ")" + cr
                    +   "       song:Play()" + cr
                    +   "   end" + cr + cr
                    +   "   action Update(number seconds)" + cr )
                    

        s8:AddText(     "       //since this action is in the main game loop and called every frame it should not be inside another loop." + cr)
        s8:SetClassAttribute("comment")
        cb12:Add(s8)
        cb12:AddText(   "       song:Stream()" + cr
                    +   "   end" + cr
                    +   "end" + cr )
        
        bodyDiv:Add(preformatter:GeneratePreformattedCode(cb12))

        AddIDE(bodyDiv, "playingLongerAudio", "Activity: Playing Longer Audio", "The same thing but with a longer audio file")

        WriteNextTutorial(bodyDiv)
        Add(bodyDiv)
        PageFooter footer
        Add(footer)
    end

    action GetMetaDescription returns text
        return "Learn how to code sound applications using the Quorum programming language."
    end

    action GetSummaryName returns text
        return "Sound"
    end

    action GetImageLocation returns text
        return "/media/Sound.gif"
    end

    action GetName returns text
        return "Playing Longer Audio: Streaming"
    end

    action GetShortDescription returns text
        return "how to play audio files Quorum"
    end

    action GetLocation returns text
        return "tutorials/dsp/audioplaylongeraudiotutorial.html"
    end

    action GetLinkName returns text
        return "Playing Longer Audio"
    end

    action GetSectionName returns text
        return "Data Science"
    end
end