use Libraries.Web.Page.WebPage
use Libraries.Web.Page.LineBreak
use Libraries.Web.Page.MetaData
use Libraries.Web.Page.Division
use Libraries.Web.Page.UnorderedList
use Libraries.Web.Page.Bold
use Libraries.Web.Page.CodeBlock
use Libraries.Web.Page.ListItem
use Libraries.Web.Page.Paragraph
use Libraries.Web.Page.PreformattedText
use Libraries.Web.Page.Comment
use Libraries.Web.Page.Span
use Libraries.Web.Page.MainContent
use Libraries.Web.Page.Header

class InheritanceTutorial is Lesson
    text dq = ""
    dq = dq:GetDoubleQuote()
    text cr = ""
    cr = cr:GetCarriageReturn()
    Preformatter preformatter //helper

    action Setup
        LineBreak break //will be used multiple times
        
        //html header stuff
        SetWebPageTitle("Inheritance - Quorum Programming Language")
        AddDefaultHeaders()

        //Figure out getting main regions properly.
        MainContent mainRegion
        AdjustMain(mainRegion)

        //page header
        Header headDiv
        headDiv:SetClassAttribute("pageHeader outermostContainer")
        headDiv:AddHeading("Tutorial: Inheritance in Quorum", 1)
        headDiv:AddText("Quorum uses inheritance for establishing relationships between classes.")
        Add(headDiv)

        Division bodyDiv
        bodyDiv:SetClassAttribute("bodyContainer")
        bodyDiv:AddHeading("On Inheritance", 2)
        bodyDiv:AddParagraph(   "We start our exploration of inheritance, a 
            method for reusing code commonly seen in many programming languages, 
            with an example on inheriting from Quorum’s math class. Imagine, for 
            example, there were certain mathematical operators that we wanted to 
            use. One way we could access the actions in Math would be to 
            instantiate a Math object and use it. Another would be to inherit 
            from it. There are good reasons we might want to do either one, but 
            first, we examine what we type:")

        bodyDiv:Add(preformatter:GeneratePreformattedCode(  
                "use Libraries.Compute.Math" + cr 
            +   "class Main is Math" + cr
            +   "     action Main" + cr
            +   "          output " + dq + "I am inheriting from Math!" + dq + cr
            +   "     end" + cr
            +   "end" + cr))

        AddSideBySideIDE(bodyDiv, "inheritance1", "Activity: Try Inheritance", 
            "Try inheriting from Math")

        bodyDiv:AddParagraph("Notice that we accomplish this by first putting 
            in a use statement for the Math class, indicating where it lives. 
            Second, after the definition of our class, Main, we use the keyword 
            is and then the name of the class we want to inherit from. Next, 
            we define our Main action normally and, just so something appears 
            in our console for this example, an output statement.")

        bodyDiv:AddParagraph("Now that we have inherited from the Math class, 
            we can access all of its actions. Here is a list of the actions 
            Math provides by default:")

        bodyDiv:AddLink("Quorum's Math Class","https://quorumlanguage.com/Libraries/Compute/Math.php")

        bodyDiv:AddParagraph("Since we have inherited from Math we can call 
            one of the Math actions even though we did not define them. For 
            example, the following example returns 4.0:")

        bodyDiv:Add(preformatter:GeneratePreformattedCode(  
                "use Libraries.Compute.Math" + cr 
            +   "class Main is Math" + cr
            +   "     action Main" + cr
            +   "          number value = RaiseToPower(2, 2)" + cr
            +   "          output value" + cr
            +   "     end" + cr
            +   "end" + cr))

        AddSideBySideIDE(bodyDiv, "inheritance2", "Activity: Try using a Parent Action", 
            "Try inheriting the Raise to Power action from Math")

        bodyDiv:AddParagraph("The key here is to realize that the RaiseToPower 
            action, the parameters here making its meaning 2-squared, can be 
            called because the Math class has such an action. By inheriting, 
            we are asking the Math class for permission to use it. Because this 
            action is public, Math gives permission.")

        //the section on inheriting a single class
        bodyDiv:AddHeading("Overriding Actions", 2)
        bodyDiv:AddParagraph("In addition to using actions from the Math class, 
            we can also add our own and even change the meaning of some of the 
            parent actions. This is a common thing to do. If we are using the 
            Game class to write games in Quorum, we might override the Update 
            action in order to change what happens on each frame of animation 
            in our game. Or, if we were overriding the Array class, we might 
            change the way arrays are sorted if we have a faster or better 
            approach. Here is an example of inheriting and calling an action 
            from the Random class:")

        bodyDiv:Add(preformatter:GeneratePreformattedCode(  
                "use Libraries.Compute.Random" + cr 
            +   "class Main is Random" + cr
            +   "     action Main" + cr
            +   "          integer value = RandomInteger()" + cr
            +   "          output value" + cr
            +   "     end" + cr
            +   "end" + cr))

        AddSideBySideIDE(bodyDiv, "inheritance3", "Activity: Try Inheriting from Random", 
            "Try inheriting the RandomInteger action from Random")

        bodyDiv:AddParagraph("By default, the RandomInteger action returns a 
            number between 0 and the maximum integer on the system, which is 
            2,147,483,646. Suppose, however, that we needed this action to 
            return a different maximum number. Normally, this would not be 
            desirable, as people using our class like the Random class might 
            not expect it. Point being: inheritance involves design and there 
            are good ones and bad ones. Being able to spot the difference, in 
            this case a relatively weak design, takes practice and iteration. 
            Here is our example, now with action overriding:")

        bodyDiv:Add(preformatter:GeneratePreformattedCode(  
                "use Libraries.Compute.Random" + cr 
            +   "class Main is Random" + cr
            +   "     action Main" + cr
            +   "          integer value = RandomInteger()" + cr
            +   "          output value" + cr
            +   "     end" + cr
            +   "     action RandomInteger returns integer" + cr
            +   "          return RandomInteger(1000)" + cr
            +   "     end" + cr
            +   "end" + cr))


        AddSideBySideIDE(bodyDiv, "inheritance4", "Activity: Try Inheriting from Random", 
            "Try overriding the RandomInteger action")

        bodyDiv:AddParagraph("In this case, the RandomInteger returns integer 
            action now provides numbers between 0 and 1000. Even though we have 
            inherited from Random, users of our class will receive our 
            functionality, not that of the parent class.")
        
        bodyDiv:AddHeading("Inheriting from Multiple Classes", 2)

        bodyDiv:AddParagraph("Programming languages differ significantly in 
            how inheritance works. Some programming languages, like Java, have 
            different styles of inheritance as compared to C++ or other 
            languages. Unfortunately, how this impacts programmers in 
            practice is poorly understood in the academic 
            literature (e.g., which systems are easier to use, by whom, and 
            under what replicable conditions?). Quorum’s system is called 
            multiple inheritance, although it differs significantly from 
            well-known systems of multiple inheritance in languages like C++.")

        bodyDiv:AddParagraph("If we wanted to inherit from two classes in 
            Quorum, we create a comma separated list of the names in the class 
            definition, like so:")

        bodyDiv:Add(preformatter:GeneratePreformattedCode(  
                "use Libraries.Compute.Random" + cr 
            +   "use Libraries.Compute.Math" + cr
            +   "class Main is Math, Random" + cr
            +   "     action Main" + cr
            +   "          integer value = RandomInteger()" + cr
            +   "          output value" + cr
            +   "" + cr
            +   "          value = AbsoluteValue(-5)" + cr
            +   "          output value" + cr
            +   "     end" + cr
            +   "end" + cr))

        AddSideBySideIDE(bodyDiv, "inheritance5", "Activity: Try Inheriting from two classes", 
            "Try inheriting from Math and Random")

        bodyDiv:AddParagraph("In this case, notice that we can now call 
            actions from the Random class, like RandomInteger, and from the 
            Math class, like AbsoluteValue. This process works in a similar 
            way conceptually to how many programming languages, like Java, 
            C#, or C++, use inheritance. The difference is in how we access 
            variables from the parents (Math and Random).")

        bodyDiv:AddHeading("Accessing Parent Variables", 2)

        bodyDiv:AddParagraph("The Math class in Quorum has variables inside of 
            it, including some common constants like pi. To access them, we 
            need to tell Quorum where the variable comes from. So, if we wanted 
            to output the value of pi to the screen, we could do it like so:")

        bodyDiv:Add(preformatter:GeneratePreformattedCode(  
                "use Libraries.Compute.Random" + cr 
            +   "use Libraries.Compute.Math" + cr
            +   "class Main is Math, Random" + cr
            +   "     action Main" + cr
            +   "          number pi = parent:Math:pi" + cr
            +   "          output pi" + cr
            +   "     end" + cr
            +   "end" + cr))

        AddSideBySideIDE(bodyDiv, "inheritance6", "Activity: Try Inheriting Parent Variables", 
            "Try using variables stored in the Math parent")

        bodyDiv:AddParagraph("The crucial point here is that we first use a 
            special keyword, called parent, and then provide which class we 
            want to obtain a variable from. In this case, that is Math. Then, 
            we say which variable we would like to obtain, in this case pi, 
            and then return and output it. This process works the same 
            regardless of the parent and the type. Only public variables 
            from the parent can be accessed.")

        bodyDiv:AddParagraph("Inheritance can be a complicated topic and 
            students in college level courses may spend a few weeks or more 
            going over the various details, depending on the language and its 
            quirks. Even once the mechanics are understood, there are many 
            ways in which we can use inheritance for advanced designs 
            (e.g., design patterns). While the tutorial here can get us 
            started using the feature in Quorum, it can be used for far more 
            advanced functionality than hinted at here and is an important 
            part of the game engine.")

        WriteNextTutorial(bodyDiv)
        mainRegion:Add(bodyDiv)
        Add(mainRegion)
        PageFooter footer
        Add(footer)
    end

    action GetMetaDescription returns text
        return "Learn about the inheritance system in the Quorum programming language."
    end

    action GetSummaryName returns text
        return "Inheritance"
    end

    action GetImageLocation returns text
        return "/media/basics/inheritance.png"
    end

    action GetName returns text
        return "inheritance"
    end

    action GetShortDescription returns text
        return "how to use the inheritance and polymorphism"
    end

    action GetLocation returns text
        return "tutorials/language/inheritance.html"
    end
end
