use Libraries.Web.Page.WebPage
use Libraries.Web.Page.LineBreak
use Libraries.Web.Page.MetaData
use Libraries.Web.Page.Division
use Libraries.Web.Page.UnorderedList
use Libraries.Web.Page.Bold
use Libraries.Web.Page.CodeBlock
use Libraries.Web.Page.ListItem
use Libraries.Web.Page.Paragraph
use Libraries.Web.Page.PreformattedText
use Libraries.Web.Page.Comment
use Libraries.Web.Page.Span
use Libraries.Web.Page.MainContent
use Libraries.Web.Page.Header

class CommentsTutorial is Lesson
    text dq = ""
    dq = dq:GetDoubleQuote()
    text cr = ""
    cr = cr:GetCarriageReturn()
    Preformatter preformatter //helper

    action Setup
        LineBreak break //will be used multiple times
        
        //html header stuff
        SetWebPageTitle("Notes and comments - Quorum Programming Language")
        AddDefaultHeaders()

        //Figure out getting main regions properly.
        MainContent mainRegion
        AdjustMain(mainRegion)

        //page header
        Header headDiv
        headDiv:SetClassAttribute("pageHeader outermostContainer")
        headDiv:AddHeading("Tutorial: Commenting Code", 1)
        headDiv:AddText("How to write notes to remind ourselves of what code does.")
        Add(headDiv)

        Division bodyDiv
        bodyDiv:SetClassAttribute("bodyContainer")
        bodyDiv:AddHeading("Comments", 2)
        bodyDiv:AddParagraph(   "Since computer code can be esoteric, "
                            +   "programmers sometimes write notes to help sort out what a piece of code does. "
                            +   "We do this using comments in two ways: an "
                            +   "1) end-of-line comment and a 2) multiline comment.")

 
        //the section on end-of-line comments
        bodyDiv:AddHeading("End-of-line comments", 2)
        bodyDiv:AddParagraph(   "An end-of-line comment tells the computer to ignore any text after the comment indicator. "
                            +   "We can also put an end-of-line comment on its own line. "
                            +   "For an end-of-line comment, we start with two forward slashes, like so:")
        CodeBlock cb1
        Span s1
        s1:AddText("// This is a comment." + cr )
        s1:SetClassAttribute("comment")
        cb1:Add(s1)
        bodyDiv:Add(preformatter:GeneratePreformattedCode(cb1))

        bodyDiv:AddParagraph("Anything after the slashes will be ignored by the compiler.")


        //the section on multiline
        bodyDiv:AddHeading("Multiline Comments", 2)
        bodyDiv:AddParagraph(   "A multiline comment can include  many lines. "
                            +   " To insert a multiline comment, we "
                            +   "start with a forward slash and an asterisk and end with an asterisk and a forward slash. Here is an example:")
        CodeBlock cb2
        Span s2
        s2:AddText("/* This is" + cr + " a comment. */")
        s2:SetClassAttribute("comment")
        cb2:Add(s2)
        bodyDiv:Add(preformatter:GeneratePreformattedCode(cb2))

        bodyDiv:AddParagraph("Quorum will ignore any text between the beginning of the comment and the end of the comment, even if it spans multiple lines.")
  


        //the section on uses of comments
        bodyDiv:AddHeading("Uses of Comments", 2)
        bodyDiv:AddParagraph(   "In addition to explaining code, comments can serve other purposes. "
                            +   "One such purpose is temporarily turning off code for testing. For instance, "
                            +   "suppose we wrote the following code:")
        bodyDiv:Add(preformatter:GeneratePreformattedCode(  "class Main" + cr
                                                        +   "   action Main" + cr
                                                        +   "       integer i = 15" + cr
                                                        +   "       integer j = 25" + cr
                                                        +   "       integer k = 35" + cr
                                                        +   "       output i " + cr
                                                        +   "       output j " + cr
                                                        +   "       output k " + cr
                                                        +   "   end" + cr
                                                        +   "end" + cr))
        bodyDiv:AddParagraph(   "Here is an example of commenting out a few lines of code:")

        CodeBlock cb3
        Span s3
        cb3:AddText("class Main" + cr
                +   "   action Main" + cr
                +   "       integer i = 15" + cr )
        s3:AddText( "       //integer j = 25" + cr 
                +   "       //integer k = 35" + cr)
        s3:SetClassAttribute("comment")
        cb3:Add(s3)
        cb3:AddText("       output i" + cr
                +   "   end" + cr
                +   "end" + cr)
        bodyDiv:Add(preformatter:GeneratePreformattedCode(cb3))

  
        bodyDiv:AddParagraph("or with a multiline comment:")

        CodeBlock cb4
        Span s4
        cb4:AddText("class Main" + cr
                +   "   action Main" + cr
                +   "       integer i = 15" + cr )
        s4:AddText( "       /*integer j = 25" + cr 
                +   "       integer k = 35*/" + cr)
        s4:SetClassAttribute("comment")
        cb4:Add(s4)
        cb4:AddText("       output i" + cr
                +   "   end" + cr
                +   "end" + cr)
        bodyDiv:Add(preformatter:GeneratePreformattedCode(cb4))

 
        bodyDiv:AddParagraph("This way, the compiler won't see the code but it will still be in our file. Below " +
            " is a development environment where we can try out comments. Now, " +
            "these comments will obviously not do anything, but the point is to try " +
            "them out to practice how it works.")


        CodingOnline usesOfCommentsIDE
        usesOfCommentsIDE:SetUniqueIdentifier("usesOfCommentsIDE")
        usesOfCommentsIDE:SetDevelopmentLabel("Activity: Try comments")
        usesOfCommentsIDE:Setup()
        usesOfCommentsIDE:GetIDESubtitle():AddText("Try the above code blocks.")
        bodyDiv:Add(usesOfCommentsIDE)

        WriteNextTutorial(bodyDiv)
        mainRegion:Add(bodyDiv)
        Add(mainRegion)
        PageFooter footer
        Add(footer)
    end

    action GetMetaDescription returns text
        return "Learn about how write notes or comments when using Quorum programming language."
    end

    action GetSummaryName returns text
        return "Comments"
    end

    action GetImageLocation returns text
        return "/media/basics/comments.png"
    end

    action GetName returns text
        return "comments"
    end

    action GetShortDescription returns text
        return "how to make notes and documentation to help us understand the code"
    end

    action GetLocation returns text
        return "tutorials/language/comments.html"
    end
end