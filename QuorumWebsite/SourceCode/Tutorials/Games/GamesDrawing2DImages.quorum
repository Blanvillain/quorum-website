use Libraries.Web.Page.WebPage
use Libraries.Web.Page.LineBreak
use Libraries.Web.Page.MetaData
use Libraries.Web.Page.Division
use Libraries.Web.Page.UnorderedList
use Libraries.Web.Page.Bold
use Libraries.Web.Page.CodeBlock
use Libraries.Web.Page.ListItem
use Libraries.Web.Page.Paragraph
use Libraries.Web.Page.PreformattedText
use Libraries.Web.Page.Comment
use Libraries.Web.Page.Span
use Libraries.Web.Page.MainContent
use Libraries.Web.Page.Header

class GamesDrawing2DImages is Lesson
    text dq = ""
    dq = dq:GetDoubleQuote()
    text cr = ""
    cr = cr:GetCarriageReturn()
    Preformatter preformatter //helper

    action Setup
        LineBreak break //will be used multiple times
        
        //html header stuff
        SetWebPageTitle("Drawing Images in 2D - Quorum Programming Language")
        AddDefaultHeaders()

        //Figure out getting main regions properly.
        MainContent mainRegion
        AdjustMain(mainRegion)

        //page header
        Header headDiv
        headDiv:SetClassAttribute("pageHeader outermostContainer")
        headDiv:AddHeading("Tutorial: Drawing Images", 1)
        headDiv:AddText("This tutorial tells you how to draw images on the screen in 2 dimensions with Quorum")
        Add(headDiv)

        Division bodyDiv
        bodyDiv:SetClassAttribute("bodyContainer")

        

        //the section on drawing images
        bodyDiv:AddHeading("Drawing Images", 2)

        bodyDiv:AddText(             "In addition to shapes, we can also display pre-made images on the screen. This is useful if we want to use external programs like ")
        bodyDiv:AddLink("Photoshop", "http://www.photoshop.com")
        bodyDiv:AddText(             " or ")
        bodyDiv:AddLink("Gimp", "http://www.gimp.org")
        bodyDiv:AddText(             " (a free alternative) to make images ahead of time or if we want to use digital photos we've taken. "
                            +   "Similarly, there is a large amount of free art on the Internet that can be used for commercial or non-commercial purposes "
                            +   "(under the creative commons license, for example)." )

        bodyDiv:AddParagraph(   "Quorum can load such images in two ways, by using: 1) a Drawable directly, or 2) an ImageSheet (a set of combined images). "
                            +   "First let us discuss using a Drawable to load images." )
        bodyDiv:AddParagraph(   "To load an image we take the same approach we used before by creating a drawable object first:")


        bodyDiv:Add(preformatter:GeneratePreformattedCode(  "Drawable logo"))
        bodyDiv:AddParagraph(   "Then we can load an image file stored on our hard drive by calling the Load method of the Drawable class. "
                            +   "For this example, we will load the logo for the Quorum Hour of Code:")
        bodyDiv:AddImage("/media/games/hourofcode.png", "This is an image of the Hour of code Quorum logo")
        bodyDiv:AddParagraph(   "This image is licensed under Creative Commons, so please feel free to download and use it for your own purposes. "
                            +   "To load this image, we first need to make a copy of it and place it in the directory of our project. It does not matter where in our project we place it,"
                            +   " but for this example we made a new folder named " + dq + "assets" + dq + " in the main project directory:")
        bodyDiv:AddImage("/media/games/assets.jpg", "This is an image of the assets in the main project directory")
        bodyDiv:AddParagraph(   "To load the image into our " + dq + "logo" + dq + " drawable object, in our CreateGame action we simply call the Load method of the Drawable class "
                            +   "with the path and filename of the file in our project and then call the Add action. "
                            +   "(Notice that the path of the file is relative to the root of our main project directory.):")
        bodyDiv:Add(preformatter:GeneratePreformattedCode(  "logo:Load(" + dq + "assets\hourofcode.png" + dq + ")" + cr
                                                        +   "Add(logo)"))
        bodyDiv:AddParagraph(   "So the whole game code looks like this:")
        bodyDiv:Add(preformatter:GeneratePreformattedCode(  "use Libraries.Game.Game" + cr
            +   "use Libraries.Game.Graphics.Drawable" + cr + cr
            +   "class Main is Game" + cr
            +   "    Drawable logo" + cr + cr
            +   "    action Main" + cr
            +   "        StartGame()" + cr
            +   "    end" + cr + cr
            +   "    action CreateGame" + cr
            +   "        logo:Load(" + dq + "assets\hourofcode.png" + dq + ")" + cr
            +   "        Add(logo)" + cr
            +   "    end" + cr + cr
            +   "    action Update(number seconds)" + cr
            +   "    end" + cr
            +   "end"))
        bodyDiv:AddParagraph(   "...and when we Run the program (F6) we get a game window that looks something like this:")
        bodyDiv:AddImage("/media/games/displayImage.png", "This is an image of the display image when you run")
        bodyDiv:AddParagraph(   "Alternatively, we could use a file class to load the image file into the Drawable. In that case, our code would look like this:")
        bodyDiv:Add(preformatter:GeneratePreformattedCode(  "use Libraries.Game.Game" + cr
            +   "use Libraries.Game.Graphics.Drawable" + cr
            +   "use Libraries.System.File" + cr + cr
            +   "class Main is Game" + cr
            +   "    Drawable logo" + cr + cr
            +   "    action Main" + cr
            +   "        StartGame()" + cr
            +   "    end" + cr + cr
            +   "    action CreateGame" + cr
            +   "        File file" + cr
            +   "        file:SetPath(" + dq + "assets\hourofcode.png" + dq + ")" + cr
            +   "        logo:Load(file)" + cr
            +   "        Add(logo)" + cr
            +   "    end" + cr + cr
            +   "    action Update(number seconds)" + cr
            +   "    end" + cr
            +   "end"))

                                                            
        AddIDE(bodyDiv, "gamesDrawing2DImagesIDE", "Activity: Draw an image", 
            "Try the above code block.", "examples/Games/Drawing2DImages/Main.quorum")


        WriteNextTutorial(bodyDiv)
        mainRegion:Add(bodyDiv)
        Add(mainRegion)
        PageFooter footer
        Add(footer)
    end

    action GetMetaDescription returns text
        return "Learn about how to draw to the screen in 2D using the Quorum programming language."
    end

    action GetSummaryName returns text
        return "Drawing 2D Images"
    end

    action GetImageLocation returns text
        return "/media/games/Drawing2D.png"
    end

    action GetName returns text
        return "Drawing 2D images"
    end

    action GetShortDescription returns text
        return "how to draw 2D images"
    end

    action GetLocation returns text
        return "tutorials/games/drawing2DImages.html"
    end
end