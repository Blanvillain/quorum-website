use Libraries.Web.Page.all
use Libraries.Containers.Array

class RemoveUndefined is Lesson
    text dq = ""
    dq = dq:GetDoubleQuote()
    text cr = ""
    cr = cr:GetCarriageReturn()
    Preformatter preformatter //helper

    action Setup
        //html header stuff
        SetWebPageTitle("Transformations - Quorum Programming Language")
        AddDefaultHeaders()

        //Figure out getting main regions properly.
        Array<Division> divs = InitializeTutorialPage()
        Division bodyDiv = divs:Get(1)
        Division onPageDiv = divs:Get(2)



        //page header
        Header headDiv
        headDiv:SetClassAttribute("flex flex-col gap-y-2")
        Heading h_heading = headDiv:AddHeading("Removing Undefined Values", 1)
        h_heading:AddClassAttribute("text-3xl font-bold")
        h_heading:AddAttribute("data-text", "Removing Undefined Values")
        AddToHeadingArray(h_heading)

        headDiv:AddText("This tutorial demonstrates how to remove values in our datasets")
        bodyDiv:Add(headDiv)

        
        Section section_rm
        section_rm:AddClassAttribute("flex flex-col gap-y-2")
        bodyDiv:Add(section_rm)
        AddHeading(section_rm, "Removing Undefined Values", 2)


       
        section_rm:AddParagraph("When cleaning a data set, it is tempting to open excel, or some other spreadsheet program, and to manually adjust a file. For many kinds of data, this is both normal and ok. For scientific data, however, we often want to create a record of exactly what we did. Otherwise, other scientists cannot reproduce our steps. ")
        
        section_rm:AddParagraph("As such, let us first write a computer program that loads our data, then removes any rows that have undefined values in them. This is not always the operation we want. Removing rows is acceptable only if it makes sense given the research questions we are trying to answer. For example, if we are trying to answer questions about a particular country in our data set, but we remove that row, then this would not make sense. ")


        section_rm:AddParagraph("In other cases, removing rows is fine. Consider if we were looking at census data, where millions of people put in their information. In such a case, while removing rows where a person did not fill out certain information could distort conclusions, when looking at millions of people, this removal might be okay. Again, it depends on what questions we are trying to answer and whether our assumptions are fair.")
        
        section_rm:AddParagraph("In our case, because many individuals have not filled out headings such as 'Other monetary comp' or 'Currency - other,' they may be less meaningful to our dataset as a whole so it may be okay to remove them. By removing such headings, our data can be less cluttered with columns that have empty entries.")
        
        section_rm:AddParagraph("To accomplish this, we use the RemoveUndefinedRowsTransform class, which does the work for us. This class first makes a copy of our DataFrame, so it does not destroy anything, then returns us the copy. We use it by passing the transform to the DataFrame. We also have a brief description of how to use these functions for our dataset.")
        
         WebTable table = AddWebTable("Removing Undefined Values Helper Functions")
        TableBody tableBody = AddTableBody()
        
        Array<text> tableHeaderArray
        tableHeaderArray:Add("Class / Action")
        tableHeaderArray:Add("Description") 
        tableHeaderArray:Add("Usage")
      
        TableHeader thead = AddTableHeaderRow(tableHeaderArray)
        table:Add(thead)        

         Array<text> rubricText1
        rubricText1:Add("RemoveUndefinedRowsTransform transformedObject")
        rubricText1:Add("This class will find any rows that are left blank and will remove them from our dataset. We will have to declare our object and then create a new DataFrame to save this changed data. From this we use our original DataFrame to clean the dataset. Note, this is only the object created.")
        rubricText1:Add("RemoveUndefinedRowsTransform transform")
        tableBody:Add(AddTableBodyRow(rubricText1))

        Array<text> rubricText2
        rubricText2:Add("DataFrame:Transform(RemoveUndefinedRowsTransform  transform)")
        rubricText2:Add("This Transform function belongs to the DataFrame class and takes in a RemoveUndefinedRowsTransform object which will help clean our dataset by removing unnecessary rows.")
        rubricText2:Add("RemoveUndefinedRowsTransform transform
DataFrame clean = frame:Transform(transform)")
 tableBody:Add(AddTableBodyRow(rubricText2))

        table:Add(tableBody)
        section_rm:Add(table)

        section_rm:AddParagraph("What we want to do first is load our dataset which we do using our DataFrame object. In this case, we created a DataFrame and called it, 'frame.' Then we want to load in our AskAManager.csv file using the Load(text fileLocation) action within the DataFrame class.")

        section_rm:Add(preformatter:GeneratePreformattedCode("//Create a DataFrame, which is essentially a table that understands 
//more information about the data that is being loaded.
DataFrame frame

//This loads data relative to the project, so put the AskAManager.csv file in the Data/Miscellaneous folder
frame:Load("+dq+"../Data/Miscellaneous/AskAManager.csv"+dq+")
"))

        section_rm:AddParagraph("Next, we will need to create a new transformation object, RemoveUndefinedRowsTransform and we will call it transform. We also want to create another DataFrame object to hold our modified 'frame'. This new DataFrame will be called 'clean' and we will be using the Transform function on 'frame' and pass in our RemoveUndefinedRowsTransform object as the parameter.")

        section_rm:Add(preformatter:GeneratePreformattedCode("RemoveUndefinedRowsTransform transform
DataFrame clean = frame:Transform(transform)
"))

        section_rm:AddParagraph("Below is an example of the full code:")

        section_rm:Add(preformatter:GeneratePreformattedCode("
//We need the DataFrame class to load in files for Data Science operations.
use Libraries.Compute.Statistics.DataFrame
use Libraries.Compute.Statistics.Transforms.RemoveUndefinedRowsTransform

//Create a DataFrame, which is essentially a table that understands 
//more information about the data that is being loaded.
DataFrame frame

//This loads data relative to the project, so put the AskAManager.csv file in the Data/Miscellaneous folder
frame:Load("+dq+"../Data/Miscellaneous/AskAManager.csv"+dq+")

//This class transforms data by removing any rows that contain undefined
//values. It is now always what we want, but can be useful
RemoveUndefinedRowsTransform transform
DataFrame clean = frame:Transform(transform)

//We can save the frame or output it to the console, like we are doing here.
output clean:ToText()"))


          Section section_try_yourself
    section_try_yourself:AddClassAttribute("flex flex-col gap-y-2")
    bodyDiv:Add(section_try_yourself)
    Heading h2_try_yourself = section_try_yourself:AddHeading("Try it Yourself!", 2)
    h2_try_yourself:AddAttribute("data-text", "Try it Yourself!")
    h2_try_yourself:AddClassAttribute("text-2xl font-bold")
    AddToHeadingArray(h2_try_yourself)
    section_try_yourself:AddAttribute("data-text", "Try it Yourself!")
    section_try_yourself:AddParagraph("Press the blue run button to execute the code in the code editor. Press the red stop button to end the program. Your program will work when the console outputs "+dq+"Build Successful!"+dq+"")
    
    CodeForIDE code4
    code4:AddFileUrl("Main.quorum", "examples/Transformations/RemoveUndefined.quorum")
    IDE ide3 = AddIDE()
    ide3:SetLightTheme()
    ide3:SetCodeForIDE(code4:GetJsonArray())
    ide3:SetIdeTitle("Removing Undefined Value")
    section_try_yourself:Add(ide3)
  
Paragraph para
        
para:AddText("To view the program we have made, we can ")

     Link link2
        link2:AddText("download the program file")
        link2:SetAddress("https://github.com/qorf/curriculum/blob/main/Reference/DataScience/Transformations/RemoveUndefinedValues.quorum")
        link2:SetTarget(link2:newTab)
        link2:SetClassAttribute("underline text-quorum-blue-150")
        para:Add(link2)
         section_try_yourself:Add(para)
                
        Section nextSection
        WriteNextTutorial(nextSection)
        bodyDiv:Add(nextSection)
        onPageDiv:Add(CreateHeadingTableOfContents())
    end

    action AddKeywords
        MetaData keywords
        keywords:AddAttribute("name", "keywords")
        keywords:AddAttribute("content", GetDataScienceDefaultKeywords() + 
        ", data transformation, dplyr, reshape, data cleaning, remove null, null, replace null, undefined,
        remove empty values, replace empty values, empty, replace empty")
        AddMetaData(keywords)
    end

    action GetMetaDescription returns text
        return "Learn about removing undefined values in the Quorum programming language."
    end

    action GetSummaryName returns text
        return "removing undefined values with DataFrames"
    end

    action GetName returns text
        return "removing undefined values "
    end

    action GetShortDescription returns text
        return "removing undefined values"
    end

    action GetLocation returns text
        return "tutorials/Transformations/RemovingUndefined.html"
    end

    action GetLinkName returns text
        return "Removing Undefined Values"
    end

    action GetSectionName returns text
        return "Data Transformations"
    end
end