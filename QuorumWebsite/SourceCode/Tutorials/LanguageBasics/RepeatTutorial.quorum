use Libraries.Web.Page.WebPage
use Libraries.Web.Page.LineBreak
use Libraries.Web.Page.MetaData
use Libraries.Web.Page.Division
use Libraries.Web.Page.UnorderedList
use Libraries.Web.Page.Bold
use Libraries.Web.Page.CodeBlock
use Libraries.Web.Page.ListItem
use Libraries.Web.Page.Paragraph
use Libraries.Web.Page.PreformattedText
use Libraries.Web.Page.Comment
use Libraries.Web.Page.Span
use Libraries.Web.Page.MainContent
use Libraries.Web.Page.Header

class RepeatTutorial is Lesson
    text dq = ""
    dq = dq:GetDoubleQuote()
    text cr = ""
    cr = cr:GetCarriageReturn()
    Preformatter preformatter //helper

    action Setup
        LineBreak break //will be used multiple times
        
        //html header stuff
        SetWebPageTitle("Repeating - Quorum Programming Language")
        AddDefaultHeaders()

        //Figure out getting main regions properly.
        MainContent mainRegion
        AdjustMain(mainRegion)

        //page header
        Header headDiv
        headDiv:SetClassAttribute("pageHeader outermostContainer")
        headDiv:AddHeading("Tutorial: Repeat", 1)
        headDiv:AddText("How to do things over and over and over and ...")
        Add(headDiv)

        Division bodyDiv
        bodyDiv:SetClassAttribute("bodyContainer")
        bodyDiv:AddHeading("repeat &#60expression&#62 times", 2)
        bodyDiv:AddParagraph(   "The Quorum programming language uses the word repeat to describe the idea of doing something 0 or more times. "
                            +   "This process, sometimes called looping, is an important 
                            concept to learn and understand in programming.
            For example, here is a program that outputs the word Quorum 10 times:")
        bodyDiv:Add(preformatter:GeneratePreformattedCode("repeat 10 times" + cr
                                                        + "     output " + dq + "Quorum" + dq + cr 
                                                        + "end"))
        bodyDiv:AddParagraph(   "We can only repeat something a whole number of times, using integers. While this is the case, 
            we can express the integer in both simple and complex ways. For example, we might try the following:")

        CodeBlock cb1
        Span s1
        Span s2
        Span s3
        Span s4
        s1:AddText("//create an integer named a with the value of 1" + cr )
        s1:SetClassAttribute("comment")
        s2:AddText("//create an integer named b with the value of 4" + cr )
        s2:SetClassAttribute("comment")
        s3:AddText("//repeat any number of times we wish, in this case by using an expression" + cr )
        s3:SetClassAttribute("comment")
        s4:AddText("    //output the two values" + cr )
        s4:SetClassAttribute("comment")
        cb1:Add(s1)
        cb1:AddText("integer a = 1" + cr )
        cb1:Add(s2)
        cb1:AddText("integer b = 4" + cr )
        cb1:Add(s3)
        cb1:AddText("repeat (b / a + (b - 5)) times" + cr )
        cb1:Add(s4)
        cb1:AddText("   output a" + cr
                +   "   output b" + cr
                +   "end"  )
        bodyDiv:Add(preformatter:GeneratePreformattedCode(cb1))

        CodingOnline repeatTimesIDE
        repeatTimesIDE:SetUniqueIdentifier("repeatTimesIDE")
        repeatTimesIDE:SetDevelopmentLabel("Activity: Try repeat times")
        repeatTimesIDE:Setup()
        repeatTimesIDE:GetIDESubtitle():AddText("Try the above code blocks.")
        bodyDiv:Add(repeatTimesIDE)

        //the section on repeat while expression
        bodyDiv:AddHeading("repeat while &#60expression&#62", 2)

        bodyDiv:AddParagraph(   "There are three ways we can repeat a construct: repeat times, repeat while, and repeat until. 
            In the next version of repeat, repeat while, we are asking Quorum to 
            continue executing as long as an expression is true, in this case while variable a is less than 15. 
            Then, on each iteration of our repeat, we add one to a, which means it will eventually stop." 
                            +   "The following example shows this idea:")


        CodeBlock cb2
        Span s5
        Span s6
        Span s7
        Span s8
        s5:AddText("//create an integer named a and set it to 0" + cr )
        s5:SetClassAttribute("comment")
        s6:AddText("//keep looping, but only if a is less than 15" + cr )
        s6:SetClassAttribute("comment")
        s7:AddText("    //add one to a. If we did not have this line, we would loop forever" + cr )
        s7:SetClassAttribute("comment")
        s8:AddText("    //send the value of a to the console" + cr )
        s8:SetClassAttribute("comment")
        cb2:Add(s5)
        cb2:AddText("integer a = 0" + cr )
        cb2:Add(s6)
        cb2:AddText("repeat while a < 15" + cr )
        cb2:Add(s7)
        cb2:AddText("   a = a + 1" + cr )
        cb2:Add(s8)
        cb2:AddText("   output a" + cr
                +   "end"  )
        bodyDiv:Add(preformatter:GeneratePreformattedCode(cb2))

        bodyDiv:AddParagraph("In this example, a will eventually become larger than 15 and the loop will stop executing.")

        CodingOnline repeatWhileIDE
        repeatWhileIDE:SetUniqueIdentifier("repeatWhileIDE")
        repeatWhileIDE:SetDevelopmentLabel("Activity: Try repeat while")
        repeatWhileIDE:Setup()
        repeatWhileIDE:GetIDESubtitle():AddText("Try the above code blocks.")
        bodyDiv:Add(repeatWhileIDE)

        //the section on repeat until
        bodyDiv:AddHeading("repeat until &#60expression&#62", 2)

        bodyDiv:AddParagraph(   "In this final version of the repeat construct, 
        we will learn about repeat until. Repeat until is just like repeat while, 
        except that the expression must evaluate to false in order to keep executing. 
        As such, using a similar example, with repeat until, the loop will have the same
        behavior as before, except that the condition is flipped.
        ")

        CodeBlock cb3
        Span s9
        cb3:AddText("integer a = 0" + cr )
        s9:AddText("//This will execute so long as a is not greater than 15" + cr )
        s9:SetClassAttribute("comment")
        cb3:Add(s9)
        cb3:AddText("repeat until a >= 15" + cr
            +   "   a = a + 1" + cr
            +   "   output a" + cr 
            +   "end" )
        bodyDiv:Add(preformatter:GeneratePreformattedCode(cb3))

        bodyDiv:AddParagraph("These various styles of loops can be used in our 
            programs freely.")
  
        CodingOnline repeatUntilIDE
        repeatUntilIDE:SetUniqueIdentifier("repeatUntilIDE")
        repeatUntilIDE:SetDevelopmentLabel("Activity: Try repeat until")
        repeatUntilIDE:Setup()
        repeatUntilIDE:GetIDESubtitle():AddText("Try the above code blocks.")
        bodyDiv:Add(repeatUntilIDE)

        WriteNextTutorial(bodyDiv)
        mainRegion:Add(bodyDiv)
        Add(mainRegion)
        PageFooter footer
        Add(footer)
    end

    action GetMetaDescription returns text
        return "Learn about how to tell the computer to repeat itself using the Quorum programming language."
    end

    action GetSummaryName returns text
        return "Repeat"
    end

    action GetImageLocation returns text
        return "/media/basics/repeat.png"
    end

    action GetName returns text
        return "repetition"
    end

    action GetShortDescription returns text
        return "how to do operations over and over again"
    end

    action GetLocation returns text
        return "tutorials/language/repeat.html"
    end
end