use Libraries.Web.Page.Division
use Libraries.Web.Page.Heading
use Libraries.Web.Page.Paragraph
use Libraries.Web.Page.Label
use Libraries.Web.Page.TextArea
use Libraries.Web.Page.Button
use Libraries.Web.Page.PreformattedText
use Libraries.Web.Page.Select
use Libraries.Web.Page.Option
use Libraries.Web.Page.UnorderedList

class AllInOneIDE is Division
    text uniqueIdentifier = ""
    Heading title
    Paragraph subtitle
    UnorderedList subtitleList
    TextArea inputArea
    Button button
    PreformattedText outputArea
    boolean shouldHaveDropDownMenu = false
    boolean shouldUseListSubtitle = false
    Select selectMenu
    text ideLabel = ""

    action GetDevelopmentLabel returns text
        return ideLabel
    end

    action SetDevelopmentLabel(text label)
        ideLabel = label
    end

    action Setup()
        me:AddClassAttribute("allInOneIde")
        me:SetIdentifier(uniqueIdentifier + "Ide")
        
        Label labelForIDE
        labelForIDE:AddAttribute("for", uniqueIdentifier + "IdeInput")
        labelForIDE:AddText(ideLabel)
        title:Add(labelForIDE)
        title:SetScale(2)
        title:SetClassAttribute("allInOneIdeTitle")
        Add(title)

        if shouldHaveDropDownMenu
            selectMenu:SetIdentifier(uniqueIdentifier + "IdeDropDownMenu")
            selectMenu:SetClassAttribute("allInOneIdeDropDownMenu")
            Add(selectMenu)
        end
        if shouldUseListSubtitle = false
            subtitle:SetClassAttribute("allInOneIdeSubtitle")
            Add(subtitle)
        else
            subtitleList:SetClassAttribute("allInOneIdeSubtitle")
            Add(subtitleList)
        end
        
        //accessibility label
        Heading inputLabel
        inputLabel:SetScale(2)
        inputLabel:SetClassAttribute("hidden")
        inputLabel:AddText("Code Area")
        Add(inputLabel)
        
        inputArea:SetClassAttribute("allInOneIdeInput")
        inputArea:SetIdentifier(uniqueIdentifier + "IdeInput")
        inputArea:SetSpellcheck(false)
        inputArea:AddAttribute("role", "textbox")              //aria attribute
        inputArea:AddAttribute("aria-multiline", "true")        //aria attribute
        Add(inputArea)

        
        button:SetClassAttribute("allInOneIdeButton")
        button:SetIdentifier(uniqueIdentifier + "IdeButton")
        button:SetFormTarget("#")
        button:AddText("Run Program")
        Add(button)
        
        //accessibility label
        Heading outputLabel
        outputLabel:SetScale(2)
        outputLabel:SetClassAttribute("hidden")
        outputLabel:AddText("Output Area")
        Add(outputLabel)

        outputArea:SetClassAttribute("allInOneIdeOutput")
        outputArea:SetIdentifier(uniqueIdentifier + "IdeOutput")
        outputArea:AddAttribute("role", "region")
        outputArea:AddAttribute("aria-atomic", "true")
        outputArea:AddAttribute("aria-label", "output of Development Environment")
        outputArea:AddAttribute("aria-live", "assertive")
        Add(outputArea)
    end

    action GetUniqueIdentifier() returns text
        return uniqueIdentifier
    end

    action SetUniqueIdentifier(text id)
        uniqueIdentifier = id
//        inputArea:SetIdentifier(uniqueIdentifier + "IdeInput")
//        button:SetIdentifier(uniqueIdentifier + "IdeButton")
//        inputArea:SetIdentifier(uniqueIdentifier + "IdeOutput")
    end

    action SetShouldHaveDropDownMenu(boolean b)
        shouldHaveDropDownMenu = b
    end

    action SetShouldUseListSubtitle(boolean b)
        shouldUseListSubtitle = b
    end

    action GetSelectMenu() returns Select
        return selectMenu
    end

    action GetIDETitle() returns Heading
        return title
    end

    action GetIDESubtitle() returns WebTag
        if shouldUseListSubtitle = false
            return subtitle
        else
            return subtitleList
        end
    end

    action SetIDESubtitle(Paragraph p)
        subtitle = p
    end

    action SetIDESubtitle(UnorderedList ul)
        subtitleList = ul
    end

    action GetIDEInputArea() returns TextArea
        return inputArea
    end

    action GetIDEButton() returns Button
        return button
    end

    action GetIDEOutputArea returns PreformattedText
        return outputArea
    end
end
