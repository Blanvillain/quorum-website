package Libraries.Web
use Libraries.Containers.Array
use Libraries.Web.Page.WebTable
use Libraries.Web.Page.TableHeader
use Libraries.Web.Page.TableBody
use Libraries.Web.Page.TableRow
use Libraries.Web.Page.TableHeaderCell
use Libraries.Web.Page.TableData

class HotkeyTable
    text name = ""
    Array<Hotkey> keys
    action Add(Hotkey key)
        keys:Add(key)
    end

    action Add(text name, text key, text description)
        Hotkey value
        value:SetName(name)
        value:SetDescription(description)
        value:SetKey(key)
        keys:Add(value)
    end

    action Generate returns WebTable
        WebTable table
        TableBody body
        //make a header row
        TableRow header
        TableHeaderCell nameHeader
        nameHeader:AddText("Name")
        
        TableHeaderCell keyHeader
        keyHeader:AddText("Key")
       
        TableHeaderCell descriptionHeader
        descriptionHeader:AddText("Description")

        header:Add(nameHeader)
        header:Add(keyHeader)
        header:Add(descriptionHeader)
        body:Add(header)


        i = 0
        repeat while i < keys:GetSize()
            Hotkey key = keys:Get(i)
            TableRow row
            TableData nameCell
            nameCell:AddText(key:GetName())

            TableData keyCell
            keyCell:AddText(key:GetKey())

            TableData descriptionCell
            descriptionCell:AddText(key:GetDescription())
            
            row:Add(nameCell)
            row:Add(keyCell)
            row:Add(descriptionCell)

            body:Add(row)
            i = i + 1
        end

        table:Add(body)
        return table
    end
    action GetName returns text
        return name
    end

    action SetName(text name)
        me:name = name
    end

end