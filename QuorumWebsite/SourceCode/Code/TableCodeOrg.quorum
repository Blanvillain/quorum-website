use Libraries.Web.Page.Division
use Libraries.Web.Page.Heading
use Libraries.Web.Page.Paragraph
use Libraries.Web.Page.OrderedList
use Libraries.Web.Page.UnorderedList
use Libraries.Web.Page.Attribute

class TableCodeOrg is Division
    text tableID = ""
    text tableHeading = ""
    text tableHeadingParagraph = ""
    OrderedList lessons
    UnorderedList bigQList
    UnorderedList enduringUList
    UnorderedList optionalList

    action SetLessonStart(integer value)
        Attribute attribute
        attribute:SetName("start")
        attribute:SetValue("" + value)
        lessons:Add(attribute)
    end

    action SetTableID(text tableID)
        me:tableID = tableID
    end
    action SetTableHeading(text tableHeading)
        me:tableHeading = tableHeading
    end
    action SetTableHeadingParagraph(text tableHeadingParagraph)
        me:tableHeadingParagraph = tableHeadingParagraph
    end
    action AddLesson(text lessonName, text lessonLink)
        lessons:AddListItemLink(lessonName, lessonLink)
    end
    action AddBigQ(text bigQText)
        bigQList:AddListItem(bigQText)
    end
    action AddEnduringU(text standardText)
        enduringUList:AddListItem(standardText)
    end
    action AddOptionalLesson(text lessonName, text lessonLink)
        if lessonLink = ""
            optionalList:AddListItem(lessonName)
        else
            optionalList:AddListItemLink(lessonName, lessonLink)
        end
    end
    action GenerateTableCodeOrg()
        //full table div
        me:SetClassAttribute("lessonTable")
        me:SetIdentifier(tableID)

        //left division of track table
        Division leftDiv
        leftDiv:SetClassAttribute("leftLessonBox")

        //left heading division
        Division tableHeadingDiv
        tableHeadingDiv:SetClassAttribute("tableHeader")
        Heading tableHeader
        tableHeader:SetClassAttribute("tableHeading")
        tableHeader:AddText(tableHeading)
        tableHeader:SetScale(1)
        Paragraph p
        p:AddText(tableHeadingParagraph)
        tableHeadingDiv:Add(tableHeader)
        tableHeadingDiv:Add(p)
        leftDiv:Add(tableHeadingDiv)

        //add the lessons to the left lesson list
        leftDiv:Add(lessons)

        //right division of track table
        Division rightDiv
        rightDiv:SetClassAttribute("rightLessonBox")

        //right big questions division
        Division bigQDiv
        bigQDiv:SetClassAttribute("standardsBox")

        Heading bigQHeader
        bigQHeader:SetClassAttribute("tableHeading")
        bigQHeader:SetScale(1)
        bigQHeader:AddText("Big Questions")
        bigQDiv:Add(bigQHeader)

        //add big questions list
        bigQDiv:Add(bigQList)

        //right CSP division
        Division enduringUDiv
        enduringUDiv:SetClassAttribute("standardsBox")
        Heading enduringUHeader
        enduringUHeader:SetClassAttribute("tableHeading")
        enduringUHeader:SetScale(1)
        enduringUHeader:AddText("Enduring Understandings")
        enduringUDiv:Add(enduringUHeader)

        //add CSP standards
        enduringUDiv:Add(enduringUList)

        //right optional lesson division
        Division optionalLessonDiv
        optionalLessonDiv:SetClassAttribute("optionalLessonBox")

        Heading optionalHeader
        optionalHeader:SetClassAttribute("tableHeading")
        optionalHeader:SetScale(1)
        optionalHeader:AddText("Optional Lessons")
        optionalLessonDiv:Add(optionalHeader)

        //add optional lesson list
        optionalLessonDiv:Add(optionalList)

        rightDiv:Add(bigQDiv)
        rightDiv:Add(enduringUDiv)
        rightDiv:Add(optionalLessonDiv)

        me:Add(leftDiv)
        me:Add(rightDiv)

    end
end