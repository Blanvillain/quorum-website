use Libraries.Web.Page.all
use Libraries.Containers.Array
use Libraries.System.File

class HourOfCodeAstro is Lesson
    text dq = ""
    dq = dq:GetDoubleQuote()
    text cr = ""
    cr = cr:GetCarriageReturn()
    integer ACTIVITY = 0
    text ACTIVITY_NAME = ""

    Preformatter preformatter //helper

    action Setup(integer activity, text subtitle, Division instructions, Division challenge)
        me:ACTIVITY = activity
        me:ACTIVITY_NAME = subtitle

        instructions:AddClassAttribute("flex flex-col gap-y-2")
        challenge:AddClassAttribute("flex flex-col gap-y-2")

        LineBreak break //will be used multiple times
        File file
        
        //html header stuff
        SetWebPageTitle("Astronomy Hour of Code Activity " + activity + " | Quorum Programming Language")
        AddDefaultHeaders()

        //Figure out getting main regions properly.
        MainContent mainRegion
        AdjustMain(mainRegion)

        //page header
        Array<Division> divs = InitializeTutorialPage()
        Division leftDiv = divs:Get(0)
        Division bodyDiv = divs:Get(1)
        Division rightDiv = divs:Get(2)

        leftDiv:AddClassAttribute("w-1/5")
        bodyDiv:AddClassAttribute("w-3/5")
        rightDiv:AddClassAttribute("hidden")

        Header headDiv
        headDiv:SetClassAttribute("flex flex-col gap-y-2")
        AddHeading(headDiv, "Astronomy Hour of Code", 1)

        headDiv:AddText("Activity " + activity + ": " + subtitle)
        bodyDiv:Add(headDiv)

        Section section_instructions
        AddHeading(section_instructions, "Instructions:", 2)
        if activity >= 16
            AddHeading(section_instructions, "(Advanced Lesson)", 3)
        end
        section_instructions:Add(instructions)


        Section section_challenge
          AddHeading(section_challenge, "Exploration Challenge (Optional):", 2)
          section_challenge:Add(challenge)

          CodeForIDE code
          code:AddFileUrl("Main.quorum", "html/hourofcode/astro/code" + activity + ".quorum")
          IDE ide = AddIDE()
          ide:SetLightTheme()
          ide:SetCodeForIDE(code:GetJsonArray())
          ide:SetIdeTitle("Type your code below...")
        section_challenge:Add(ide)

        bodyDiv:Add(section_instructions)
        bodyDiv:Add(section_challenge)

        Section nextSection
        WriteNextTutorial(nextSection)
        bodyDiv:Add(nextSection)
        rightDiv:Add(CreateHeadingTableOfContents())
    end

    action GetMetaDescription returns text
        return "Try an accessible astronomy themed Hour of Code in the Quorum programming language."
    end

    action GetSummaryName returns text
        return "Astronomy Hour of Code | Activity " + ACTIVITY
    end

    action GetImageLocation returns text
        return ""
    end

    action GetName returns text
        return "Astronomy Hour of Code | Activity " + ACTIVITY
    end

    action GetShortDescription returns text
        return "Accessible astronomy themed Hour of Code"
    end
end
