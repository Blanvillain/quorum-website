use Libraries.Web.Page.Division
use Libraries.Web.Page.CodeBlock
use Libraries.Web.Page.Paragraph

class HoCAstro15 is HourOfCodeAstro
    integer ACTIVITY = 15
    text SUBTITLE = "Playing an Audio File from the Server"

    action Setup()
        Setup(ACTIVITY, SUBTITLE, GetInstructions(), GetChallenge())
    end

    action GetInstructions() returns Division
        text dq = ""
        dq = dq:GetDoubleQuote()
        Division instructions
        Preformatter pre
        
        instructions:AddParagraph("Now we are going to learn how to add sounds to our program. Audio can be very useful to provide the user feedback about what the computer is doing, particularly to non-sighted users. To begin, we are just going to create an <b>Audio</b> object, load a sound and then play it.  Before we can start working with Audio objects though, we need to tell the compiler where to find the object in a library. In Quorum, the library is located at <b>Libraries.Sound.Audio</b> and we reference it with the <b>use</b> command. We always need to put the <b>use</b> commands at the top of the program before we try to reference any objects in the library. So, the first statement in our program should look like this:")

        CodeBlock cb1
        cb1:AddText("use Libraries.Sound.Audio")
        instructions:Add(pre:GeneratePreformattedCode(cb1))
        
        instructions:AddParagraph("Next, we will create an <b>Audio</b> object by declaring it, similar to how we created a variable with a type and a name. Let's create an <b>Audio</b> object called <b>clicksound</b> like this:")

        CodeBlock cb2
        cb2:AddText("Audio clickSound")
        instructions:Add(pre:GeneratePreformattedCode(cb2))
        
        instructions:AddParagraph("Before we can do anything with the <b>clickSound</b> object, we need to load an audio file into it. We do that with the <b>Load</b> command. We call the <b>Load</b> command by putting a " + dq + ":" + dq + " after the name of the object and typing <b>Load</b> with the path of the file in parenthesis. There is a sound called <b>click.wav</b> on the webserver located at the path <b>" + dq + "media/astro/" + dq + "</b> so to load it into the <b>clickSound</b> object, we can use this command:")

        CodeBlock cb3
        cb3:AddText("clickSound:Load( " + dq + "media/astro/click.wav" + dq + ")")
        instructions:Add(pre:GeneratePreformattedCode(cb3))
        
        instructions:AddParagraph("Now all we have to do is tell the sound to play. To do that, we can call the <b>Play</b> action of our <b>Audio</b> object. If we call an action on an object it always needs to have parenthesis after the name of the action and the action name is capitalized, like this:")

        CodeBlock cb4
        cb4:AddText("clickSound:Play()")
        instructions:Add(pre:GeneratePreformattedCode(cb4))

        instructions:AddParagraph("When you are done, click the green Run Program button and listen to the sound.")

        return instructions
    end

    action GetChallenge() returns Division
        Division challenge
        Paragraph challengeText       
        challengeText:SetIdentifier("astro-challenge")
        challengeText:AddText("For this Exploration Challenge, put the Play() statement in a repeat loop to play the audio more than once. If you want to make your program more interactive, get input from the user to ask how many times the sound should play.")
        challenge:Add(challengeText)
        return challenge
    end

    action GetLocation returns text
        return "hourofcode/astro" + ACTIVITY + ".html"
    end

    action GetLinkName returns text
        return "Part " + ACTIVITY + ": " + SUBTITLE
    end

    action GetSectionName returns text
        return "Astronomy"
    end
end
