use Libraries.Web.Page.Division
use Libraries.Web.Page.CodeBlock
use Libraries.Web.Page.Paragraph

class HoCAstro13 is HourOfCodeAstro
    integer ACTIVITY = 13
    text SUBTITLE = "Convert Right Ascension to Degrees"

    action Setup()
        Setup(ACTIVITY, SUBTITLE, GetInstructions(), GetChallenge())
    end

    action GetInstructions() returns Division
        text dq = ""
        dq = dq:GetDoubleQuote()
        Division instructions
        Preformatter pre
        
        instructions:AddParagraph("In order for us to give our virtual telescope movement commands in the computer, we have to convert the Right Ascension numbers into degrees. Since a full turn of a circle is 360 degrees we can translate the hours and minutes and seconds to degrees. We can construct a formula for this by converting the hours and minutes and seconds that the user input into the total number of hours using decimal values and then converting the total number of hours to degrees.")

        instructions:AddParagraph("Let's start by creating a <b>totalHours</b> variable of type <b>number</b> (since we will need decimal places) and assign it the value contained in <b>hours</b> from the user input:")

        CodeBlock cb1
        cb1:AddText("number totalHours = hours")
        instructions:Add(pre:GeneratePreformattedCode(cb1))

        instructions:AddParagraph("Next, we know that there are 60 minutes in every hour, so we can convert minutes to hours by dividing the value contained in the variable <b>minutes</b> by 60 and adding it to <b>totalHours</b>. We will put parentheses around the minutes calculation to tell the computer to do that part first. ")

        instructions:AddParagraph("We can also use the variable <b>totalHours</b> twice in the same equation (on the left and right side of the equation) because the computer will use the original value in the computer's memory in the calculation on the right side of the equation first and then write the new value into memory on the left side of the equation after the calculation is complete.  The next line in the conversion will look like this: ")

        CodeBlock cb2
        cb2:AddText("totalHours = totalHours + (minutes / 60)")
        instructions:Add(pre:GeneratePreformattedCode(cb2))

        instructions:AddParagraph("We can finish our calculation for <b>totalHours</b> by converting seconds to hours and adding that to <b>totalHours</b>. Since there are 60 minutes in an hour and 60 seconds in a minute, there are 3600 seconds per hour, so we can make this calculation like this:")

        CodeBlock cb3
        cb3:AddText("totalHours = totalHours + (seconds / 3600)")
        instructions:Add(pre:GeneratePreformattedCode(cb3))

        instructions:AddParagraph("Now <b>totalHours</b> contains the total number of hours as a decimal value and we can convert that number to degrees. Since there are 360 degrees in a circle and 24 hours in a day, we can calculate the number of degrees per hour by dividing 360 by 24, which gives us 15. So if we multiply <b>totalHours</b> by 15, we have the number of degrees.  We can write this in code like this:")

        CodeBlock cb4
        cb4:AddText("number degrees = totalHours * 15")
        instructions:Add(pre:GeneratePreformattedCode(cb4))

        instructions:AddParagraph("To output the answer, we can combine the value of degrees with a text string to make our answer in a nice format. We do that like this:")

        CodeBlock cb5
        cb5:AddText("output " + dq + "The number of degrees are: " + dq + " + degrees")
        instructions:Add(pre:GeneratePreformattedCode(cb5))

        instructions:AddParagraph("When you are ready, select the green Run Program button below the editor on the right (second in the tab order).")

        return instructions
    end

    action GetChallenge() returns Division
        Division challenge
        Paragraph challengeText       
        challengeText:SetIdentifier("astro-challenge")
        challengeText:AddText("The code we wrote in this activity has more lines than we need to use. Try to re-write the code to put the formula on a single line without using the variable <b>totalHours</b>.")
        challenge:Add(challengeText)
        return challenge
    end

    action GetLocation returns text
        return "hourofcode/astro" + ACTIVITY + ".html"
    end

    action GetLinkName returns text
        return "Part " + ACTIVITY + ": " + SUBTITLE
    end

    action GetSectionName returns text
        return "Astronomy"
    end
end
