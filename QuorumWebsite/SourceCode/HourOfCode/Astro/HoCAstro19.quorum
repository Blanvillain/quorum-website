use Libraries.Web.Page.Division
use Libraries.Web.Page.CodeBlock
use Libraries.Web.Page.Paragraph

class HoCAstro19 is HourOfCodeAstro
    integer ACTIVITY = 19
    text SUBTITLE = "Adding Keyboard Controls and Audio"
    action Setup()
        Setup(ACTIVITY, SUBTITLE, GetInstructions(), GetChallenge())
    end

    action GetInstructions() returns Division
        text dq = ""
        dq = dq:GetDoubleQuote()
        text cr = ""
        cr = cr:GetCarriageReturn()
        Division instructions
        Preformatter pre
        
        instructions:AddParagraph("In this lesson we are going to add keyboard controls to the skybox so you can move the camera around. In order to give the sense of movement to unsighted users we are going to play the click sound every time the camera moves 10 degrees. In order to use movement we are going to use and object in the game engine called an <b>InputMonitor</b> which monitors things like keyboard presses and mouseclicks that the user makes and allows our program to react when something is pressed. We will also use an object called a <b>KeyboardEvent</b> which identifies the particular key that is pressed.")

        instructions:AddParagraph("In the starting code, we have already included the <b>use</b> statement that you will need and we declared the <b>InputMonitor</b> and the <b>KeyboardEvent</b> objects called <b>monitor</b> and <b>keys</b>. Our first line of code, will just check if the monitor detects if the right key is pressed with this statement:")
        
        CodeBlock cb1
        cb1:AddText("if monitor:IsKeyPressed(keys:RIGHT)" + cr)
        instructions:Add(pre:GeneratePreformattedCode(cb1))

        instructions:AddParagraph("Inside this conditional block we are goint to set the new camera position to display by first calculating the rotation, which is called yaw in the game engine like this:")

        CodeBlock cb2
        cb2:AddText("    yaw = yaw + 30 * seconds" + cr)
        cb2:AddText("    camera:SetYawPitchRoll(yaw, pitch, roll)" + cr)
        instructions:Add(pre:GeneratePreformattedCode(cb2))

        instructions:AddParagraph("In order to play a click every 10 degrees, we have to check if the yaw has changed by more than 10 since the last time we played a click and if it has, we play the click and then save the current value of the yaw in the <b>lastYaw</b> variable and output the value to the console.")

        CodeBlock cb3
        cb3:AddText("    if yaw > (lastYaw + 10) or yaw < (lastYaw - 10)" + cr)
        cb3:AddText("        clickSound:Play()" + cr)
        cb3:AddText("        lastYaw = math:Round(yaw,-1)" + cr)
        cb3:AddText("        output lastYaw" + cr)
        cb3:AddText("    end" + cr)
        instructions:Add(pre:GeneratePreformattedCode(cb3))


        instructions:AddParagraph("Now all we have to do is close the first conditional statement with an <b>end</b>:")

        CodeBlock cb4
        cb4:AddText("end")
        instructions:Add(pre:GeneratePreformattedCode(cb4))

        instructions:AddParagraph("Now you are ready to Run your program and rotate the camera with the right arrow key to view your skybox. You can also press the space key to have the current rotation displayed in the console. In order to have the game engine respond to your keyboard input you need to make sure that the visual output window has focus by either clicking on it or tabbing in to it.")

        return instructions
    end

    action GetChallenge() returns Division
        Division challenge
        Paragraph challengeText       
        challengeText:SetIdentifier("astro-challenge")
        challengeText:AddText("Add keyboard controls for the other arrow keys so that you can move the camera in other directions. You will have to change the formula slightly to go left instead of right. To move up and down you will need to set the pitch and not the yaw variable. If you want to make the camera move faster or slower, change the constant value in the yaw formula which is set at 30 to start. If you add a control for the pitch, see what happens if you press up until you feel like you flip over backwards.")
        challenge:Add(challengeText)
        return challenge
    end

    action GetLocation returns text
        return "hourofcode/astro" + ACTIVITY + ".html"
    end

    action GetLinkName returns text
        return "Part " + ACTIVITY + ": " + SUBTITLE
    end

    action GetSectionName returns text
        return "Astronomy"
    end
end
