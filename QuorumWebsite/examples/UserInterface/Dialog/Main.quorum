use Libraries.Game.Game
use Libraries.Interface.Controls.Button
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Controls.Dialog
use Libraries.Interface.Controls.Control
use Libraries.Game.Graphics.Color
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Behaviors.Controls.DialogHideBehavior

class Main is Game, Behavior

    Dialog dialog
    action Main
        StartGame()
    end

    action CreateGame

        // setup dialog
        Dialog dialog        
        Button close
        
        dialog:SetName("My Dialog")
        dialog:SetPixelWidth(300)
        dialog:SetModal(true)

        Color c
        Icon diaIcon
        diaIcon:LoadFilledRectangle(300, 300, c:LightGray())

        Control panel
        panel:SetPercentageWidth(1)
        panel:SetPixelHeight(300)
        panel:Add(diaIcon)
        panel:Add(close)
        dialog:Add(panel)

        DialogHideBehavior behave
        behave:SetDialog(dialog)
        close:SetBehavior(behave)

        close:SetName("Close")
        close:SetPosition(130, 140)

        me:dialog = dialog

        //make button to open dialog
        Button open
        open:SetName("Open Dialog")
        open:SetBehavior(me)
        open:SetPosition(100, 100)

        SetFocus(open)
        Add(open)
    end

    action Run(BehaviorEvent event)
        dialog:Show()
    end

    action Update(number seconds)
    end
end